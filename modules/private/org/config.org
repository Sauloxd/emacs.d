#+TITLE: Org Mode Configuration
#+AUTHOR: Florian Schrödl (floscr)
#+PROPERTY: header-args :tangle yes
#+STARTUP: org-startup-folded: showall
#+BEGIN_SRC emacs-lisp
;; -*- lexical-binding: t -*-
#+END_SRC

* Configuration
** Required Packages

#+BEGIN_SRC elisp
(require 'dash)
(require 'f)
(require 's)
#+END_SRC

** Additional files

#+BEGIN_SRC elisp
(if (featurep! +org-noter)        (load! "+org-noter"))
(if (featurep! +org-reading-list) (load! "+org-reading-list"))
(if (featurep! +org-web-tools)    (load! "+org-web-tools"))
(if (featurep! +org-tags)         (load! "+org-tags"))
#+END_SRC

** Utils
*** Setup Org Mode Hook

#+BEGIN_SRC elisp
(defun +org*setup-org-mode-hook ()
  "Setup org mode buffers"
  (display-line-numbers-mode -1)
  (hl-line-mode -1)
  (line-number-mode -1)
  (visual-line-mode)
  (let ((visual-fill-column-center-text nil)
        (visual-fill-column-width 100))
    (visual-fill-column-mode)))
#+END_SRC

*** Get Frontmost chrome url

#+BEGIN_SRC elisp
(defun +org/get-frontmost-chrome-url-raw ()
  "Get just the url for the frontmost chrome instance."
  (interactive)
  (--> (org-mac-chrome-get-frontmost-url)
       (s-match org-bracket-link-regexp it)
       (nth 1 it)))
#+END_SRC

*** Add source property from chrome

#+BEGIN_SRC elisp
(defun +org|source-properties-key-from-browser ()
  "Add the link from the frontmost chrome tab as a source property."
  (interactive)
  (org-set-property "SOURCE" (org-mac-chrome-get-frontmost-url)))
#+END_SRC

*** Archive all done tasks

#+BEGIN_SRC elisp :tangle no
(defun +org|archive-done-tasks ()
  "Archive tasks with DONE or CANCELED todo state."
  (interactive)
  (org-map-entries
   (lambda ()
     (org-archive-subtree)
     (setq org-map-continue-from (outline-previous-heading)))
   "/+{|DONE|CANCELED}" 'tree))
#+END_SRC

*** Archive and Done

#+BEGIN_SRC elisp
(defun +org|archive-and-done ()
  "Mark task as done and archive."
  (interactive)
  (org-todo "DONE")
  (org-archive-subtree))

(defun +org|agenda-archive-and-done ()
  "Mark agenda task as done and archive."
  (interactive)
  (org-agenda-todo "DONE")
  (org-agenda-archive))
#+END_SRC

*** Copy block to clipboard

#+BEGIN_SRC elisp
(defun +org|copy-block ()
  "Copies the current block to clipboard."
  (interactive)
  (org-edit-src-code)
  (clipboard-kill-ring-save (point-min) (point-max)))
#+END_SRC

*** Export As Markdown

#+BEGIN_SRC elisp
(defun +org/copy-as-markdown (&optional subtree-p)
  "Copy the current subtree as markdown to clipboard."
    (let* ((org-export-with-toc nil)
           (md (org-md-export-as-markdown nil subtree-p)))
      (kill-ring-save (point-min) (point-max))
      (kill-buffer md)))

(defun +org|copy-buffer-as-markdown ()
  "Copy the entire buffer as markdown to clipboard."
  (interactive)
  (+org/copy-as-markdown))

(defun +org|copy-subtree-as-markdown ()
  "Copy the subtree as markdown to clipboard."
  (interactive)
  (+org/copy-as-markdown t))
#+END_SRC

*** Grab Tablist from Chrome

#+BEGIN_SRC elisp
(defun +org|grab-chrome-tabs ()
  "Grab all the chrome tabs as an org list."
  (interactive)
  (let ((tabs
         (do-jxa-script
          (concat
           "Application(\"Chrome\").windows[0].tabs()"
           ".map(tab => `"
           "- [[${tab.url()}][${tab.title()}]]"
           "`)"
           ".join(\"\\n\")"))))
    (insert tabs)))
#+END_SRC

*** Org Attach
**** Download

#+BEGIN_SRC elisp
(defun +org-attach/downloads-file (file)
  "Attach a file in your downloads-directory."
  (interactive (list (read-file-name "Attach File: " downloads-dir)))
  (+org-attach/uri file))
#+END_SRC

**** Screenshot

#+BEGIN_SRC elisp
(defun +org-attach/last-screenshot ()
  "Attaches the last screenshot"
  (interactive)
  (+org-attach/file (+file/latest-file-in-dir screenshot-dir)))
#+END_SRC

*** Paste Chrome Link Fix

Fixes wrong paste behaviour where the link would be inserted directly on the character by adding a space

E.g.: (Brackets signal the cursor position)

: **[*]
: ***[]"

#+BEGIN_SRC elisp
(defun +org|insert-chrome-url ()
  "Insert a link around the current selection."
  (interactive)
  (org-insert-link nil (+org/get-frontmost-chrome-url-raw)))

(defun +org|paste-chrome-link ()
  "Paste the frontmost chrome link."
  (interactive)
  (cond
   ;; Visual Selection
   ((region-active-p) (+org|insert-chrome-url))
   ;; Normal
   (t (progn
        (unless (looking-at-p "[\s\t\n\r]") (forward-char))
        (insert " ")
        (insert (org-mac-chrome-get-frontmost-url))))))
#+END_SRC

*** Paste Markdown as org

#+BEGIN_SRC elisp
(defun +org|paste-markdown-as-org ()
  "Convert the current clipboard to markdown."
  (interactive)
  (insert (shell-command-to-string "pbpaste | pandoc -f markdown -t org")))
#+END_SRC

*** Schedule Tomorrow
:PROPERTIES:
:SOURCE:   [[https://github.com/xandeer/.doom/blob/master/modules/private/xandeer/+org.el][.doom/+org.el at master · xandeer/.doom]]
:END:

#+BEGIN_SRC elisp
(defun +org|schedule-tomorrow ()
  "Return scheduled string on tomorrow."
  (format-time-string "SCHEDULED: <%F %a>"
                      (time-add (current-time) (* 24 3600))))
#+END_SRC

*** Sort org entries

#+BEGIN_SRC elisp
(defun +org|sort-entries ()
  "Go to header and sort entries."
  (interactive)
  (org-up-element)
  (org-sort)
  (org-shifttab)
  (org-cycle))
#+END_SRC

*** Visit archive file

#+BEGIN_SRC elisp
(defun +org|visit-archive-file ()
  (interactive)
  (let ((archive-filename (car (org-archive--compute-location org-archive-location))))
    (find-file archive-filename)
    (end-of-buffer)))
#+END_SRC

*** Algin all tags

#+BEGIN_SRC elisp
(defun +org|align-all-tags ()
  "Interactive version of org-align-all-tags."
  (interactive)
  (org-align-all-tags))
#+END_SRC

*** Org get chrome tab formatted

The Github Review Title is very long,
shorten it a bit for the org capture templates.

#+BEGIN_SRC elisp
(defun +github/just-pr-title (title)
  (car (s-split " · " title)))

(defun +org/mac-chrome-get-frontmost-url-custom-format ()
  "Adaption for org-as-mac-chrome-get-frontmost-url."
    (--> (org-as-mac-chrome-get-frontmost-url)
         (s-split "::split::" it)
         (pcase it
           ((pred (s-contains? "github.com" (-first-item it)))
            (-update-at (- (length it) 1)
                        #'+github/just-pr-title
                        it))
           (_ it))
         (s-join "::split::" it)
         (org-mac-paste-applescript-links it)))
#+END_SRC

*** Custom Agenda Compare

Sort my agenda entries by a ~DATE_CREATED~ property.

#+BEGIN_SRC elisp
(setq-default +org-created-property "DATE_CREATED")
#+END_SRC

#+BEGIN_SRC elisp
(defun +org|compare-created-date-property (a b)
  "Compare two `org-mode' agenda entries, `A' and `B', by the \"CREATED\" property."
  (let* ((a-pos (get-text-property 0 'org-marker a))
         (b-pos (get-text-property 0 'org-marker b))
         (a-date (or (org-entry-get a-pos +org-created-property)
                     (format "<%s>" (org-read-date t nil "now"))))
         (b-date (or (org-entry-get b-pos +org-created-property)
                     (format "<%s>" (org-read-date t nil "now"))))
         (cmp (compare-strings a-date nil nil b-date nil nil)))
    (if (eq cmp t) nil (signum cmp))))
#+END_SRC

*** Search in notes

#+BEGIN_SRC elisp
(defun +org|search-in-notes ()
  "Search in the org directory for a file.
Projectile search is not fast reliable enough."
  (interactive)
  (counsel-notes-jump))

(defun counsel-notes-jump ()
  "Jump to a file in your notes and call org-goto right-away."
  (interactive)
  (counsel-require-program find-program)
  (let ((default-directory org-directory))
       (ivy-read "Find directory: "
                 (cdr
                  (counsel--find-return-list (split-string ". -name *.org -type f -print")))
                 :matcher #'counsel--find-file-matcher
                 :action (lambda (d)
                           (find-file (expand-file-name d))
                           (counsel-org-goto))
                 :history 'file-name-history
                 :keymap counsel-find-file-map
                 :caller 'counsel-dired-jump)))
#+END_SRC

*** Find in files

#+BEGIN_SRC elisp
(defun +org/find-in-files (file)
  "Find file in org directory."
  (find-file (f-join org-directory file)))
#+END_SRC
*** Rename Attachment

#+BEGIN_SRC elisp
(defun +org-attach|rename-attachment ()
  "Rename the attachment under the cursor."
  (interactive)
  (save-excursion
    (while (not (org-element-link-parser))
      (backward-char))
    (if-let* ((link (org-element-link-parser))
              (filename (org-element-property :path link))
              (begin (org-element-property :begin link))
              (end (org-element-property :end link))

              (path (f-join org-attach-directory filename))
              (new-path (let ((ivy--directory org-attach-directory))
                          (counsel-find-file-move path)))
              (new-filename (s-replace org-attach-directory "" new-path))
              (new-attach-link (template "[[attach:<<new-filename>>]]")))
        (progn
          (delete-region begin end)
          (insert new-attach-link)))))
#+END_SRC
*** Visual Fill Column

#+BEGIN_SRC elisp
(defun +org|toggle-visual-wrap (&optional center?)
  "Toggle text wrapping for org buffers."
  (interactive)
  (let ((visual-fill-column-center-text (or center? nil)))
    (if (bound-and-true-p visual-line-mode)
        (progn
          (visual-line-mode -1)
          (visual-fill-column-mode -1))
      (progn
        (visual-line-mode)
        (visual-fill-column-mode)))))
#+END_SRC
*** Agenda Visit Enty in Narrow Mode

#+BEGIN_SRC elisp
(defun +org/todo-state-subtask? ()
  "If the current taks item is a subtask (which is marked by [ ])."
  (if-let ((state (org-get-todo-state)))
      (string-match-p "^\\[" state)))

(defun +org|agenda-visit-entry ()
  "Visit entry and narrow to section."
  (interactive)
  (org-agenda-switch-to)
  ;; When narrowed on a subtask, narrow to the next parent task
  (if (+org/todo-state-subtask?)
      (save-excursion
        (org-up-element)
        (while (and (eq (org-get-todo-state) nil)
                    (eq (+org/todo-state-subtask?) nil))
          (org-up-element)
          (org-narrow-to-subtree)))
    (org-narrow-to-subtree)))
#+END_SRC

*** Move to shopping list

Moves a todo item from the agenda to the shopping list as a list item.

#+BEGIN_SRC elisp
(defun +org|agenda-move-to-shopping-list ()
  "Function docstring"
  (interactive)
  (save-window-excursion
    (save-excursion
      (let ((title (nth 1 (s-match "^.*TODO \\(.*\\)$" (thing-at-point 'line t))))
            (buffer (find-file org-shopping-list-file)))
        (with-current-buffer buffer
          (org-narrow-to-subtree)
          (goto-char (point-max))
          (insert (template "\n- [ ] <<title>>"))
          (org-update-checkbox-count t)
          (save-buffer)))))
  (+org|agenda-archive-and-done))
#+END_SRC
*** Export named table
:PROPERTIES:
:SOURCE:   [[https://emacs.stackexchange.com/questions/16640/can-i-export-a-specific-table-in-an-org-file-to-csv-from-the-command-line][org mode - Can I export a specific table in an org file to csv from the command line? - Emacs Stack Exchange]]
:END:

#+BEGIN_SRC elisp
(defun +org|export-named-table (name &optional dir)
  "Export table in org document with NAME property to a DIR.
Return the path of the file."
  (interactive)
  (show-all)
  (let ((case-fold-search t))
    (save-excursion
      (goto-char (point-min))
      (if (search-forward-regexp (concat "#\\+NAME: +" name) nil t)
          (progn
            (next-line)
            (let ((filename (file-truename (f-join (or dir "./") (format "%s.csv" name)))))
              (org-table-export filename "orgtbl-to-csv")
              filename))))))
#+END_SRC
*** Refile Helpers
:PROPERTIES:
:SOURCE:   [[https://mollermara.com/blog/Fast-refiling-in-org-mode-with-hydras/][Fast refiling in org-mode with hydras | Josh Moller-Mara]]
:END:

#+BEGIN_SRC elisp
(defun +org/refile (file headline &optional arg)
  (let ((pos (save-excursion
               (find-file (f-join org-directory file))
               (org-find-exact-headline-in-buffer headline))))
    (org-refile arg nil (list headline file nil pos)))
  (switch-to-buffer (current-buffer)))

(defun +org/agenda-refile (file headline &optional arg)
  (save-window-excursion
    (let ((pos (save-excursion
                (find-file (f-join org-directory file))
                (org-find-exact-headline-in-buffer headline))))
      (org-agenda-refile arg (list headline file nil pos) t))))
#+END_SRC

** Config
*** General Config
**** Org variables

#+BEGIN_SRC elisp
(setq
 org-directory (-first #'f-exists? '("~/Documents/org" "~/Documents/Org"))
 org-pinboard-dir org-directory
 org-pinboard-file (concat org-pinboard-dir "/Bookmarks/bookmarks.org")
 org-pinboard-archive-file (concat org-pinboard-dir "/Bookmarks/.archive/pinboard.org")
 org-default-notes-file (concat org-directory "/inbox.org")
 org-shopping-list-file (concat org-directory "/shoppinglist.org")
 +org-reading-list-file (concat org-directory "/reading-list.org"))

(after! org
  :config
  (add-hook! org-mode #'+org*setup-org-mode-hook)
  (setq
   org-src-window-setup 'current-window
   org-tags-column (- fill-column)
   org-image-actual-width 600
   org-default-notes-file (concat org-directory "/inbox.org")))
#+END_SRC

**** Todo State Keywords

All todo keywords are for general items.

~[ ]~ style keywords are for subtasks, that I might want to filter out from certain views.
#+BEGIN_EXAMPLE
- [!] Stands for active
- [?] Stands for waiting
- [|] Stands for cancelled
#+END_EXAMPLE

#+BEGIN_SRC elisp
(after! org
  :config
  (setq org-todo-keywords
        '((sequence "ACTIVE(a)" "NEXT(n)" "|" "DONE")
          (sequence "TODO(t)" "|" "DONE(x)")
          (sequence "[ ]([)" "[!](!)" "[-](-)" "|" "[X](])" "[|](|)")
          (sequence "PROJECT(p)" "|" "DONE")
          (sequence "NOTE(N)")
          (sequence "WAITING(w)" "LATER(l)" "SOMEDAY(s)" "|" "DONE" "CANCELLED(c)")
          (sequence "CHECK_BACK_FOR_REVIEW" "TO_BE_MERGED"))
   org-todo-keyword-faces
   '(("ACTIVE" :inherit warning :weight bold)
     ("NEXT" :inherit warning :weight bold)
     ("NOTE" :inherit warning :weight bold)
     ("WAITING" :inherit default :weight bold)
     ("CHECK_BACK_FOR_REVIEW" :inherit default :weight bold)
     ("TO_BE_MERGED" :inherit default :weight bold)
     ("SOMEDAY" :inherit default :weight bold)
     ("LATER" :inherit default :weight bold)
     ("PROJECT" :inherit 'org-todo :weight bold))))
#+END_SRC

**** Attachments

#+BEGIN_SRC elisp
(after! org
  :config
  (add-to-list 'org-link-abbrev-alist (cons "attach" (abbreviate-file-name org-attach-directory))))
#+END_SRC

**** Projectile Ignore

#+BEGIN_SRC elisp
(after! projectile
  (add-to-list 'projectile-globally-ignored-file-suffixes ".org_archive")
  (add-to-list 'projectile-globally-ignored-file-suffixes ".DS_Store"))
#+END_SRC

**** Latex

Needs ~dvisvgm~

#+BEGIN_SRC elisp
(setq org-latex-create-formula-image-program 'dvisvgm)
#+END_SRC

*** Refile Targets

#+BEGIN_SRC elisp
(defun expand-org-file-names (xs)
  (mapcar (lambda (x) (expand-file-name x org-directory)) xs))

(setq level-1-refile-targets
      (expand-org-file-names
       '("reading-list.org"
         "cooking.org"
         ;; "books.org"
         "programming.org"
         "shoppinglist.org")))

(setq max-level-2-refile-targets
      (expand-org-file-names
       '("Emacs.org"
         "art.org"
         "diary"
         "games.org"
         "hardware.org"
         "GTD.org"
         "inbox.org"
         "mealplan.org"
         "misc.org"
         "movies.org"
         "music.org"
         "osx.org"
         "personal.org"
         "podcasts.org"
         "projects.org"
         "sleep.org"
         "sports.org"
         "travel.org"
         "Work/work.org")))

(defun level-1-refile-targets () level-1-refile-targets)

(defun max-level-2-refile-targets () max-level-2-refile-targets)

(after! org
  :config
  (setq org-refile-targets (quote ((nil :maxlevel . 5)
                                   (max-level-2-refile-targets :maxlevel . 2)
                                   (level-1-refile-targets :level . 1)))
        org-agenda-refile org-agenda-files))
#+END_SRC
*** Journal

#+BEGIN_SRC elisp :tangle no
(defvar org-journal-dir-default "~/Dropbox/org/journal")
(defvar org-journal-dir-diary "~/Dropbox/org/diary")

(setq org-journal-dir org-journal-dir-default)
(setq org-journal-file-format "%Y-%m-%d")
(setq org-journal-date-prefix "#+TITLE: ")
(setq org-journal-date-format "%A, %B %d %Y")
(setq org-journal-time-prefix "* ")
(setq org-journal-time-format "")
#+END_SRC

*** Org Capture
**** Org Capture Config

#+BEGIN_SRC elisp
(setq +org-capture-frame-parameters
  `((name . "org-capture")
    (width . 120)
    (height . 35)
    (transient . t)))
#+END_SRC

**** Add created property after capture

#+BEGIN_SRC elisp
(defun +org|add-created-date-property ()
  "Add DATE_CAPTURED property to the current item."
  (interactive)
  (org-set-property +org-created-property (format-time-string  "[%Y-%m-%d %a %H:%M]")))

(add-hook 'org-capture-before-finalize-hook '+org|add-created-date-property)
#+END_SRC

**** Org Capture Templates

#+BEGIN_SRC elisp
(defun org-capture-bookmark-pair ()
  (split-string (org-as-mac-chrome-get-frontmost-url) "::split::"))

(defun org-capture-bookmark-string-url ()
  (car (org-capture-bookmark-pair)))

(defun org-capture-bookmark-string-description ()
  (cadr (org-capture-bookmark-pair)))

(after! org
  :config
  (setq org-capture-templates
        `(("t" "todo" entry
           (file org-default-notes-file)
           "* TODO %?\n")

          ("f" "File Todo" entry
           (file org-default-notes-file)
           "* TODO %?\n%a\n")

          ("c" "Calendar Event" entry
           (file+headline ,(concat org-directory "/GTD.org") "Calendar")
           "* %?\n")

          ("e" "Emacs Todo" entry
           (file+headline ,(concat org-directory "/emacs.org") "Emacs Todos")
           "* TODO %?\n")

          ("p" "Pin Bookmark" entry (file+headline org-pinboard-file "Pinboard")
           "* %(org-capture-bookmark-string-description)%?\n:PROPERTIES:\n:URL:  %(org-capture-bookmark-string-url)\n:TIME: %U\n:END:\n")

          ("rr" "Add to reading list" entry (file+headline +org-reading-list-file "Reading List")
           "* TODO %(org-mac-chrome-get-frontmost-url)%?\n")

          ("rw" "Add to watching list" entry (file+headline +org-reading-list-file "Watching List")
           "* TODO %(org-mac-chrome-get-frontmost-url)%?\n")

          ("C" "Browser" entry
           (file org-default-notes-file)
           "* TODO %(org-mac-chrome-get-frontmost-url)%?\n")

          ("ww" "Work Task" entry
           (file+headline ,(concat org-directory "/Work/work.org") "Work Todos")
           "* TODO %?\n")

          ("wc" "Work Task from Chrome" entry
           (file+headline ,(concat org-directory "/Work/work.org") "Work Todos")
           "* TODO %?\n:PROPERTIES:\n:MT_URL: %(org-mac-chrome-get-frontmost-url)\n:END:\n")

          ("wr" "Work Review" entry
           (file+headline ,(concat org-directory "/Work/work.org") "Work Todos")
           "* TODO %(+org/mac-chrome-get-frontmost-url-custom-format)%? :REVIEW:\n"))))
#+END_SRC
*** Src Block Snippets

#+BEGIN_SRC elisp :tangle no
(add-to-list 'org-structure-template-alist '("es" "#+BEGIN_SRC elisp\n?\n#+END_SRC\n"))
(add-to-list 'org-structure-template-alist '("E"  "#+BEGIN_EXAMPLE\n?\n#+END_EXAMPLE"))
(add-to-list 'org-structure-template-alist '("j"  "#+BEGIN_SRC js\n?\n#+END_SRC\n"))
(add-to-list 'org-structure-template-alist '("ps" "#+BEGIN_SRC purescript\n?\n#+END_SRC\n"))
(add-to-list 'org-structure-template-alist '("b"  "#+BEGIN_SRC bash\n?\n#+END_SRC\n"))
(add-to-list 'org-structure-template-alist '("re" "#+BEGIN_SRC reason\n?\n#+END_SRC\n"))
(add-to-list 'org-structure-template-alist '("oc" "#+BEGIN_SRC ocaml\n?\n#+END_SRC\n"))
(add-to-list 'org-structure-template-alist '("rb" "#+BEGIN_SRC ruby\n?\n#+END_SRC\n"))
(add-to-list 'org-structure-template-alist '("md" "#+BEGIN_SRC markdown\n?\n#+END_SRC\n"))
#+END_SRC

*** Agenda
**** Config
***** Agenda Files

#+BEGIN_SRC elisp
(after! org-agenda
  :config
  (setq org-agenda-files
        (--map (f-join org-directory it)
               '("GTD.org"
                 "inbox.org"
                 "calendar-family.org"
                 "Work/work.org"))))
#+END_SRC

***** Custom Date Property

#+BEGIN_SRC elisp
(after! org-agenda
  (setq-default
   org-agenda-cmp-user-defined #'+org|compare-created-date-property
   org-agenda-sorting-strategy '((agenda habit-down user-defined-up time-up priority-down category-keep)
                                 (todo priority-down category-keep user-defined-up time-up)
                                 (tags priority-down category-keep user-defined-up time-up)
                                 (search category-keep))))
#+END_SRC

**** Bindings

Enable avy/evil-motion for agenda

#+BEGIN_SRC elisp
(map! :after evil-org-agenda
      :map org-agenda-mode-map
      :m "gs" nil)
#+END_SRC

***** Motions

#+BEGIN_SRC elisp
(evil-define-key 'motion org-agenda-mode-map
  "vd" 'org-agenda-day-view
  "ds" 'org-agenda-schedule
  "vw" 'org-agenda-week-view
  "vm" 'org-agenda-month-view
  "vy" 'org-agenda-year-view)
#+END_SRC

**** Custom Agenda Commands

Helper to reset the org agenda custom commands.

#+BEGIN_SRC elisp :tangle no
(setq org-agenda-custom-commands '())
#+END_SRC

***** Helper

#+BEGIN_SRC elisp
(defun +org/add-to-agenda-custom-commands (x)
  "Add or replace X in the org-agenda-custom-commands list."
  (if-let ((item (alist-get (car x) nil nil #'equal)))
      (setf item x)
    (add-to-list 'org-agenda-custom-commands x)))
#+END_SRC

***** Opening Tag

#+BEGIN_SRC elisp
(after! org-agenda
#+END_SRC

***** Today View

#+BEGIN_SRC elisp
(+org/add-to-agenda-custom-commands
 '("d" "Day View" ((agenda "a"
                           ((org-agenda-prefix-format "  %?-12t% s")
                            (org-agenda-start-on-weekday nil)
                            (org-agenda-span 1)
                            (org-agenda-start-day ".")
                            (org-agenda-skip-scheduled-if-done t)
                            (org-agenda-sorting-strategy '(timestamp-up time-up))
                            (org-super-agenda-header-separator "")
                            (org-agenda-day-view)
                            (org-super-agenda-groups '((:name none :date today :time-grid t)
                                                       (:name "\nOverdue" :deadline past :scheduled past)
                                                       (:name "Future" :anything (:scheduled future)))))))))
#+END_SRC

***** Week View

#+BEGIN_SRC elisp
(+org/add-to-agenda-custom-commands
 '("D" "Day View" ((agenda "a"
                           ((org-agenda-prefix-format "  %?-12t% s")
                            (org-agenda-start-on-weekday nil)
                            (org-agenda-span 5)
                            (org-agenda-start-day ".")
                            (org-agenda-skip-scheduled-if-done t)
                            (org-agenda-sorting-strategy '(timestamp-up time-up))
                            (org-super-agenda-header-separator "")
                            (org-agenda-day-view)
                            (org-super-agenda-groups '((:name none :date today :time-grid t)
                                                       (:name "\nOverdue" :deadline past :scheduled past)
                                                       (:name "Future" :anything (:scheduled future)))))))))
#+END_SRC

***** All TODO Items

#+BEGIN_SRC elisp
(+org/add-to-agenda-custom-commands
 '("x" "Todo Items"
   ((agenda
     "a"
     ((org-agenda-span 3)
      (org-agenda-use-time-grid 'require-timed)
      (org-agenda-start-day ".")
      (org-agenda-show-all-dates nil)
      (org-agenda-prefix-format '((agenda . "  ")))
      (org-super-agenda-header-separator "")
      (org-agenda-sorting-strategy '(priority-down scheduled-down ts-down todo-state-down))
      (org-super-agenda-groups '((:name none :date today :time-grid t)
                                 (:name "\nOverdue" :deadline past :scheduled past)
                                 (:name "Future" :anything (:scheduled future))))))
    (tags-todo
     "-WORK"
     ((org-agenda-prefix-format "  %?-12t% s")
      (org-agenda-sorting-strategy '(user-defined-down timestamp-down todo-state-down))
      (org-super-agenda-groups '((:name "Next" :todo ("ACTIVE"))
                                 (:name nil :discard (:scheduled t))
                                 (:name "Inbox" :file-path ".*inbox.org$" :order 2)
                                 (:name "Unscheduled" :and (:todo "TODO" :scheduled nil :not (:tag "BACKLOG")) :order 1)
                                 (:name "Backlog" :tag "BACKLOG" :order 3)
                                 (:name "Overdue" :scheduled past))))))))
#+END_SRC

***** Emacs

#+BEGIN_SRC elisp
(+org/add-to-agenda-custom-commands
 '("e" "Emacs Items" alltodo ""
   ((org-agenda-prefix-format "  %?-12t% s")
    (org-agenda-sorting-strategy '(user-defined-down timestamp-down todo-state-down))
    (org-agenda-files (list (f-join org-directory "Emacs.org")))
    (org-super-agenda-groups '((:name "Active" :todo "ACTIVE")
                               (:auto-priority t)
                               (:name "Next" :todo "NEXT")
                               (:name "Backlog" :todo "TODO"))))))
#+END_SRC

***** Work
****** Todo Agenda

#+BEGIN_SRC elisp
(+org/add-to-agenda-custom-commands
 '("w" "Work Agenda"
   ((agenda "a" ((org-agenda-sorting-strategy '(deadline-up user-defined-down timestamp-down time-down))
                 (org-agenda-span 3)
                 (org-agenda-start-on-weekday t)
                 (org-agenda-show-all-dates nil)
                 (org-agenda-use-time-grid 'require-timed)
                 (org-agenda-start-day "-1d")))
    (alltodo "+WORK-EVENT"
             ((org-agenda-sorting-strategy '(todo-state-up user-defined-down timestamp-down time-down))
              (org-agenda-tag-filter-preset '("+WORK" "-EVENT"))
              (org-super-agenda-groups '((:name "In Progress" :todo ("ACTIVE" "[!]"))
                                         (:discard (:todo ("[ ]" "[|]")))
                                         (:name "Reviews" :tag "REVIEW" :order 4)
                                         (:name "DevTools" :tag "DEV_TOOLS" :order 5)
                                         (:name "Meta Work" :tag "META_WORK" :order 5)
                                         (:name "Tasks"
                                                :not (:scheduled future :todo "SOMEDAY" :todo "WAITING" :tag ("BACKLOG" "TEXT" "EMACS"))
                                                :order 3)
                                         (:name "Waiting" :todo ("WAITING" "[-]") :order 4)
                                         (:name "Backlog" :tag "BACKLOG" :todo "SOMEDAY" :order 11)
                                         (:name "Reading List" :tag "TEXT" :order 10))))))
   ((org-agenda-hide-tags-regexp "WORK\\|BACKLOG")
    (org-agenda-tag-filter-preset "+WORK")
    (org-agenda-files (--map (f-join org-directory it) '("Work/work.org" "inbox.org"))))))
#+END_SRC

****** Project Agenda

#+BEGIN_SRC elisp
(defun +org/get-parent-project ()
  (while (and (org-up-heading-safe) (not (--find (string= (org-get-todo-state) it) '("PROJECT" "ACTIVE")))))
  (--find (string= (org-get-todo-state) it) '("PROJECT" "ACTIVE")))


(after! org-super-agenda
  :init
  (org-super-agenda--def-auto-group parent-todo "their parent todo heading"
    :key-form (org-super-agenda--when-with-marker-buffer (org-super-agenda--get-marker item)
                (when (and (org-up-heading-safe) (+org/get-parent-project))
                  (org-get-heading 'notags 'notodo)))))

(+org/add-to-agenda-custom-commands
 '("p" "Project Agenda"
   ((alltodo "+WORK-EVENT"
             ((org-agenda-sorting-strategy '(todo-state-down user-defined-down timestamp-down time-down))
              (org-agenda-tag-filter-preset '("-EVENT"))
              (org-super-agenda-groups '((:name "Projects" :auto-parent-todo)
                                         (:discard (:anything t)))))))
   ((org-agenda-hide-tags-regexp "WORK\\|BACKLOG")
    (org-agenda-files (list (f-join org-directory "Work/work.org"))))))
#+END_SRC

***** Calendar

#+BEGIN_SRC elisp
(+org/add-to-agenda-custom-commands
 '("c" "Calendar" agenda ""
   ((org-agenda-span 7)
    (org-agenda-start-on-weekday nil)
    (org-agenda-start-day "-1d")
    (org-agenda-tag-filter-preset '("+CALENDAR")))))
#+END_SRC

***** Closing Tag

#+BEGIN_SRC elisp
)
#+END_SRC

*** Clocking
**** Automatically set clocking or todo state
:PROPERTIES:
:SOURCE:   [[https://github.com/magnars/dash.el#-contains-list-element][magnars/dash.el: A modern list library for Emacs]]
:END:

#+BEGIN_SRC elisp
(after! org
#+END_SRC

Clock in when the todo state has been changed to ~ACTIVE~

#+BEGIN_SRC elisp
(defun +org/org-clock-in-if-starting ()
  "Clock in when the task is marked ACTIVE."
  (when (and (string= org-state "ACTIVE")
             (not (string= org-last-state org-state)))
    (org-clock-in)))

(add-hook 'org-after-todo-state-change-hook '+org/org-clock-in-if-starting)
#+END_SRC

Clock out when the todo state is set to a delay state.

#+BEGIN_SRC elisp
(defun +org/org-clock-out-if-waiting ()
  "Clock out when the task is marked WAITING."
  (when (and (-contains? '("WAITING" "SOMEDAY" "CANCELLED") org-state)
             (equal (marker-buffer org-clock-marker) (current-buffer))
             (< (point) org-clock-marker)
             (> (save-excursion (outline-next-heading) (point))
               org-clock-marker)
             (not (string= org-last-state org-state)))
    (org-clock-out)))

(add-hook 'org-after-todo-state-change-hook '+org/org-clock-out-if-waiting)
#+END_SRC

Set the active state when clocking in.

#+BEGIN_SRC elisp
(defun +org/org-set-active-state (&optional args args)
  "Set the active state for the current item."
  (cond ((buf-line-has "PROJECT") nil)
        ((buf-line-has "\\[.\\]") (org-todo "[!]"))
        (t (org-todo "ACTIVE"))))

(advice-add #'org-clock-in :after #'+org/org-set-active-state)
#+END_SRC

#+BEGIN_SRC elisp
)
#+END_SRC

*** Tables
**** Copy Table Field

#+BEGIN_SRC elisp
(defun +org|table-copy-field ()
  "Copy a table field under the cursor."
  (interactive)
  (save-excursion
    (kill-new (s-trim (org-table-get-field)))))
#+END_SRC

** Package Config
*** Org Caldav

Calendar management using org mode and [[https://github.com/dengste/org-caldav][org-caldav]].

To sync the calendars use ~org-caldav-sync~.

#+BEGIN_SRC elisp
(use-package! org-caldav
  :after org
  :commands (org-caldav-sync)
  :config
  ;; org-caldav-url is set in secrets.el
  (setq org-caldav-calendar-id "family"
        org-caldav-inbox (f-join org-directory "calendar-family.org")
        org-caldav-save-directory doom-cache-dir
        org-icalendar-timezone "Europe/Berlin")
  (setq org-caldav-files (list org-caldav-inbox)))
#+END_SRC

*** Org QL

#+BEGIN_SRC elisp
(use-package! org-ql
  :commands (org-ql-search))
#+END_SRC

*** Org SuperAgenda
**** Config

#+BEGIN_SRC elisp
(use-package! org-super-agenda
  :after org
  :config
  (org-super-agenda-mode 1)
  ;; Disable org-super-agenda keymap which breaks evil mappings
  (setq org-super-agenda-header-map (make-sparse-keymap)))
#+END_SRC

**** Utils
***** Move between super agenda sections

#+BEGIN_SRC elisp
(defun +org-agenda/goto-super-agenda-group (forward?)
  "Move the cursor to a super agenda group."
  (let ((is-at-agenda-header
         (lambda () (eq (get-text-property (point) 'face) 'org-super-agenda-header)))
        (line-move-fn (if forward? #'next-line #'previous-line))
        (char-move-fn (if forward? #'forward-char #'backward-char)))

    ;; When the cursor is on a group already
    (if (funcall is-at-agenda-header)
        (funcall line-move-fn))

    ;; Special case for moving backward
    ;; Since moving backward would just stop at the current header
    (if (and (not forward?) (not (funcall is-at-agenda-header)))
        (progn
          (while (not (funcall is-at-agenda-header))
            (backward-char))
          (previous-line)
          (previous-line)))

    ;; Find the next header
    (while (not (funcall is-at-agenda-header))
      (funcall char-move-fn))

    ;; Move below the header to the first item
    (if (funcall is-at-agenda-header)
        (progn
          (next-line)
          (beginning-of-line)
          (back-to-indentation)))))

(defun +org-agenda|next-super-agenda-group ()
  "Move the cursor to the next super agenda group."
  (interactive)
  (+org-agenda/goto-super-agenda-group t))

(defun +org-agenda|prev-super-agenda-group ()
  "Move the cursor to the previous super agenda group."
  (interactive)
  (+org-agenda/goto-super-agenda-group nil))
#+END_SRC

*** Org Media Info

#+BEGIN_SRC elisp
(use-package! org-media-info
  :commands (org-media-insert-book org-media-insert-book-german))
#+END_SRC

** Bindings
*** Local Leader Bindings
**** Local Leader Mapping
***** Org Mode

#+BEGIN_SRC elisp
(map! :after org
      :map org-mode-map
      :localleader
      :desc  "Align Tags"                "%"  #'+org|align-all-tags
      :desc  "Archive Subtree and Done"  "A"  #'+org|archive-and-done
      :desc  "Archive Subtree"           "a"  #'org-archive-subtree
      :desc  "Copy Buffer To Markdown"   "y"  #'+org|copy-buffer-as-markdown
      :desc  "Create/Edit Todo"          "o"  #'org-todo
      :desc  "Cut Subtree"               "C"  #'org-cut-subtree
      :desc  "Deadline"                  "d"  #'org-deadline
      :desc  "Filter"                    "f"  #'org-match-sparse-tree
      :desc  "Goto Archive"              "$"  #'+org|visit-archive-file
      :desc  "Grab tabs"                 "P"  #'+org|grab-chrome-tabs
      :desc  "Paste Chrome Link"         "p"  #'+org|paste-chrome-link
      :desc  "Paste Subtree"             "P"  #'org-paste-subtree
      :desc  "Schedule Tomorrow"         "+"  #'+org|schedule-tomorrow
      :desc  "Schedule"                  "s"  #'org-schedule
      :desc  "Set source key to tab"     "k"  #'+org|source-properties-key-from-browser
      :desc  "Sort Entries"              "S"  #'+org|sort-entries
      :desc  "Tag heading"               "t"  #'+org-tags|set-tags

      (:prefix-map ("i" . "Insert")
        :desc "Inavtive Timestamp" "i" 'org-time-stamp-inactive
        :desc "Subheadeing"        "s" (λ!
                                        (call-interactively 'org-insert-subheading)
                                        (evil-insert-state)))

      (:prefix-map ("f" . "Attach File")
        :desc "Downloads File"  "d" '+org-attach/downloads-file
        :desc "File"            "f" '+org-attach/file
        :desc "Screenshot"      "s" '+org-attach/last-screenshot
        :desc "URI"             "u" '+org-attach/uri)

      (:prefix-map ("g" . "Goto")
        :desc "Org Web Link" "l" #'+org-web-tools/read-url-at-point)

      (:prefix-map ("n" . "Narrow")
        :desc "Block"                "b" #'org-narrow-to-block
        :desc "Element"              "e" #'org-narrow-to-element
        :desc "Indirect Buffer Tree" "i" #'org-tree-to-indirect-buffer
        :desc "Subtree"              "s" #'org-narrow-to-subtree
        :desc "widen"                "w" #'widen)

      (:prefix-map ("w" . "Refile")
        :desc "Dynamic"            "w" 'org-refile
        :desc "To GTD Backlog"     "b" (λ! (+org/refile "GTD.org" "GTD Backlog"))
        :desc "To GTD"             "g" (λ! (+org/refile "GTD.org" "GTD"))))
#+END_SRC

***** Agenda

#+BEGIN_SRC elisp
(map! :after evil-org-agenda
      :map org-agenda-mode-map
      :localleader
      :desc  "Set tags" "q"  #'counsel-org-tag-agenda
      (:prefix-map ("w" . "Refile")
        :desc "Dynamic"            "w" 'org-agenda-refile
        :desc "To GTD Backlog"     "b" (λ! (+org/agenda-refile "GTD.org" "GTD Backlog"))
        :desc "To GTD"             "g" (λ! (+org/agenda-refile "GTD.org" "GTD"))
        :desc "To shoppping list"  "s" #'+org|agenda-move-to-shopping-list))
#+END_SRC

**** Meta Mapping

#+BEGIN_SRC elisp
(map! :g "s-X" #'+org-capture/open-frame)

(map! :map org-mode-map
      :gni [s-return]    #'+org/insert-item-below
      :gni [s-S-return]  #'+org/insert-item-above
      :n "s-k"           #'org-metaup
      :n "s-j"           #'org-metadown)
#+END_SRC

**** Agenda Mapping
***** Evil Customization

#+BEGIN_SRC elisp
(map! :after evil-org-agenda
      :map evil-org-agenda-mode-map
      :m "RET"    #'+org|agenda-visit-entry
      :m [return] #'+org|agenda-visit-entry
      :m "K"      #'+org-agenda|prev-super-agenda-group
      :m "J"      #'+org-agenda|next-super-agenda-group
      :m "da"     #'org-agenda-archive
      :m "dA"     #'+org|agenda-archive-and-done)
#+END_SRC

***** Org Agenda Keybindings

#+BEGIN_SRC elisp
(map! :map org-agenda-mode-map
      :desc "Org Window Left" "C-h" #'evil-window-left
      :desc "Org Window Left" "C-l" #'evil-window-right)
#+END_SRC

*** Leader Bindings

#+BEGIN_SRC elisp
(map!
 :leader
 (:desc "Toggle" :prefix "t"
   :desc "Visal Fill Column"          "o" #'+org|toggle-visual-wrap
   :desc "Visal Fill Column (Center)" "O" (λ! (+org|toggle-visual-wrap)))

 (:desc "Notes" :prefix "n"
   :desc "Pinboard File"              "B" (λ! (find-file org-pinboard-file))
   :desc "Save All Org Buffers"       "S" #'org-save-all-org-buffers
   :desc "Agenda"                     "a" #'org-agenda
   :desc "Search Pinboard"            "b" #'helm-org-pinboard
   :desc "Emacs"                      "e" (λ! (+org/find-in-files "Emacs.org"))
   :desc "GTD (Home)"                 "h" (λ! (+org/find-in-files "GTD.org"))
   :desc "Inbox"                      "i" (λ! (+org/find-in-files "inbox.org"))
   :desc "Find in notes"              "n" #'+org|search-in-notes
   :desc "Reading List"               "r" #'+org-reading-list/org-open-reading-list-file
   :desc "Work"                       "w" (λ! (+org/find-in-files "Work/work.org"))
   :desc "Store Link"                 "y" #'org-store-link

   (:prefix-map ("c" . "clock")
     :desc "Timestamp Down"           "-" #'org-clock-timestamps-down
     :desc "Timestamp Up"             "=" #'org-clock-timestamps-up
     :desc "Clock Out"                "C" #'org-clock-out
     :desc "Goto Select"              "G" (λ! (org-clock-goto 'select))
     :desc "Clock In"                 "c" #'org-clock-in
     :desc "Mark Default Task"        "d" #'org-clock-mark-default-task
     :desc "Modify Effort Estimate    " "e" #'org-clock-modify-effort-estimate
     :desc "Goto Current"             "g" #'org-clock-goto
     :desc "Resolve"                  "r" #'org-resolve-clocks
     :desc "Clock In Last"            "l" #'org-clock-in-last
     :desc "Cancel"                   "x" #'org-clock-cancel)))
#+END_SRC
